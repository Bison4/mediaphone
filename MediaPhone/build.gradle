apply plugin: 'com.android.application'

android {
	compileSdkVersion 29

	defaultConfig {
		manifestPlaceholders = [providerId: 'mediaphone']
		applicationId 'ac.robinson.' + manifestPlaceholders.providerId
		buildConfigField 'String', 'PROVIDER_ID', '"' + manifestPlaceholders.providerId + '"'

		minSdkVersion 14
		targetSdkVersion 29
		versionCode 37
		versionName '1.6.1'
		// versionNameSuffix = 'beta'
		resConfigs 'en', 'es', 'fr', 'nl', 'pt', 'pl', 'ru'
	}

	buildTypes {
		all {
			buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance()
					.getTimeInMillis() + "L)"
		}

		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mediaphone.txt'

			shrinkResources true
		}

		// automatically regenerate fastlane metadata for new releases
		task generateMetadata(type: Exec) {
			doFirst {
				workingDir '../fastlane/'
				commandLine 'python3', 'xmltofastlane.py'
			}
		}
		tasks.whenTaskAdded { task ->
			if (task.name == 'assembleRelease') {
				task.finalizedBy generateMetadata
			}
		}
	}
}

dependencies {
	implementation project(':MediaUtilities')

	implementation 'com.google.android.material:material:1.1.0'
	implementation 'androidx.exifinterface:exifinterface:1.2.0' // for auto-selection of export resolution
	implementation 'androidx.core:core:1.3.0' // for FileProvider
}
