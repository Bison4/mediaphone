apply plugin: 'com.android.application'

android {
	compileSdkVersion 29

	defaultConfig {
		manifestPlaceholders = [providerId: 'mediaphone']
		applicationId 'ac.robinson.' + manifestPlaceholders.providerId
		buildConfigField 'String', 'PROVIDER_ID', '"' + manifestPlaceholders.providerId + '"'

		minSdkVersion 14
		targetSdkVersion 29
		versionCode 37
		versionName '1.6.1'
		resConfigs 'en', 'es', 'fr', 'nl', 'pt', 'pl', 'ru'
	}

	buildTypes {
		all {
			buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance()
					.getTimeInMillis() + "L)"
		}

		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mediaphone.txt'

			shrinkResources true
		}
	}
}

dependencies {
	implementation project(':MediaUtilities')

	implementation 'com.google.android.material:material:1.1.0'
	implementation 'androidx.exifinterface:exifinterface:1.1.0' // for auto-selection of export resolution
	implementation 'androidx.core:core:1.2.0' // for FileProvider

	implementation 'com.google.code.findbugs:annotations:3.0.1', {
		// Need to exclude these, or build is broken by:
		//   com.android.dex.DexException: Multiple dex files define Ljavax/annotation/CheckForNull
		exclude module: 'jsr305'
		exclude module: 'jcip-annotations'
	}
}
