apply plugin: 'com.android.application'

android {
	compileSdkVersion 27
	buildToolsVersion '27.0.3'

	defaultConfig {
		applicationId 'ac.robinson.mediaphone'
		//noinspection MinSdkTooLow
		minSdkVersion 9 // we *want* to support low SDK versions (older devices)
		targetSdkVersion 27
		versionCode 29
		versionName '1.5'
		resConfigs 'en', 'es', 'fr', 'pt', 'pl'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	buildTypes {
		debug {
			buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance()
					.getTimeInMillis() + "L)"
		}
		release {
			buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance()
					.getTimeInMillis() + "L)"

			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mediaphone.txt'

			shrinkResources true
		}
	}
}

dependencies {
	compile project(':MediaUtilities')

	//noinspection GradleDependency
	compile 'com.android.support:design:25.4.0' // we need to stick with the older library to support SDKs 9 to 13 :-(

	compile 'com.google.code.findbugs:annotations:3.0.1', {
		// Need to exclude these, or build is broken by:
		//   com.android.dex.DexException: Multiple dex files define Ljavax/annotation/CheckForNull
		exclude module: 'jsr305'
		exclude module: 'jcip-annotations'
	}
}
